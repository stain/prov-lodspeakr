PREFIX conversion: <http://purl.org/twc/vocab/conversion/>
prefix pmlp:       <http://inference-web.org/2.0/pml-provenance.owl#>
PREFIX pmlj: <http://inference-web.org/2.0/pml-justification.owl#>
prefix httpget:    <http://inference-web.org/registry/MPR/HTTP_1_1_GET.owl#> 
prefix hartigprov: <http://purl.org/net/provenance/ns#>
prefix irw:        <http://www.ontologydesignpatterns.org/ont/web/irw.owl#> 

SELECT distinct ?engine ?source ?time ?actor ?conclusion ?rule ?fromUrl 
WHERE {
  GRAPH <http://logd.tw.rpi.edu/source/lebot/dataset/golfers/version/2012-Mar-15> {
   ?nodeset
      a pmlj:NodeSet;
      pmlj:isConsequentOf ?step;
      pmlj:hasConclusion ?conclusion.
   ?step
      a pmlj:InferenceStep;
      pmlj:hasInferenceEngine ?engine;
      #assume we know the rule or we can iterate over the set of rules supported
      pmlj:hasInferenceRule   httpget:HTTP_1_1_GET;
      pmlj:hasInferenceRule   ?rule;
      pmlj:hasSourceUsage     ?sUsage;
      hartigprov:involvedActor ?actor.
   ?sUsage 
      pmlp:hasSource ?source;
      pmlp:hasUsageDateTime ?time.
    optional {?fromUrl irw:redirectsTo ?source.}
  }
  #assume know the version of the dataset
  FILTER regex(?conclusion, "/version/2012-Mar-15/source") 
}

