prefix rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix pmlp:    <http://inference-web.org/2.0/pml-provenance.owl#>
prefix void:    <http://rdfs.org/ns/void#>
prefix prov:    <http://www.w3.org/ns/prov#>
prefix example: <http://example.org/ns#>

select ?expression max(?type) as ?yes
where {
   {
      select (MAX(?m) AS ?modified) ?provdm ?provrdf ?provo
      where {
         graph ?meta { # This graph knows when stuff was retrieved.
                       # Using that, we want to find the latest retrieval.
            [] a pmlp:SourceUsage;
               pmlp:hasSource       <https://dvcs.w3.org/hg/prov/raw-file/default/ontology/ProvenanceOntology.owl>;
               pmlp:hasUsageDateTime ?m
            .
            [] void:subset ?provdm, ?provrdf, ?provo .
            ?provdm  prov:wasDerivedFrom <http://www.w3.org/TR/2012/WD-prov-dm-20120202/> .
            ?provrdf prov:wasDerivedFrom <http://www.w3.org/2011/prov/wiki/ProvRDF> .
            ?provo   prov:wasDerivedFrom <http://dvcs.w3.org/hg/prov/raw-file/default/ontology/ProvenanceOntology.owl> .
         }
      } group by ?meta ?provdm ?provrdf ?provo order by desc(?modified) limit 1
   }
   graph ?provdm {
      [] a example:Expression;
         rdf:value ?expression;
         prov:hadLocation ?order .
   }
   optional {
      graph ?provrdf {
         [] a ?type;
            rdf:value ?expression.
      }
   }
   filter(regex(?expression,'id'))
} order by ?order
